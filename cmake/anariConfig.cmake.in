## Copyright 2021 The Khronos Group
## SPDX-License-Identifier: Apache-2.0

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(CMAKE_THREAD_PREFER_PTHREAD TRUE)
set(THREADS_PREFER_PTHREAD_FLAG TRUE)
find_dependency(Threads)

include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@_Exports.cmake")

check_required_components("@PROJECT_NAME@")

if (NOT TARGET anari::anari)
  message(FATAL_ERROR "CMAKE_PREFIX_PATH or anari_DIR are pointing to an \
  ANARI-SDK build directory. Please do a full install of the SDK \
  (e.g. 'make install') and point to where you installed it \
  (CMAKE_INSTALL_PREFIX in your build of ANARI-SDK). \
  Consuming ANARI-SDK from a build directory is not supported.")
endif()

if (@INSTALL_CODE_GEN_SCRIPTS@)
  set(ANARI_CODE_GEN_ROOT
    ${CMAKE_CURRENT_LIST_DIR}/../../../@CMAKE_INSTALL_DATAROOTDIR@/anari/code_gen
  )

  include(${CMAKE_CURRENT_LIST_DIR}/cmake/anari_generate_frontend.cmake)
endif()

set(ANARI_FOUND TRUE)
