## Copyright 2021 The Khronos Group
## SPDX-License-Identifier: Apache-2.0

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@_Exports.cmake)

if (NOT TARGET anari::anari)
  message(FATAL_ERROR "CMAKE_PREFIX_PATH or anari_DIR are pointing to an \
  ANARI-SDK build directory. Please do a full install of the SDK \
  (e.g. 'make install') and point to where you installed it \
  (CMAKE_INSTALL_PREFIX in your build of ANARI-SDK). \
  Consuming ANARI-SDK from a build directory is not supported.")
endif()

set(ANARI_DATAROOTDIR
  ${CMAKE_CURRENT_LIST_DIR}/../../../@CMAKE_INSTALL_DATAROOTDIR@/anari
)

if (@INSTALL_CODE_GEN_SCRIPTS@)
  set(ANARI_CODE_GEN_ROOT ${ANARI_DATAROOTDIR}/code_gen)
  include(${CMAKE_CURRENT_LIST_DIR}/cmake/anari_generate_frontend.cmake)
  include(${CMAKE_CURRENT_LIST_DIR}/cmake/anari_generate_codegen.cmake)
  set(anari_code_gen_FOUND 1)
elseif(anari_FIND_REQUIRED_code_gen)
  message(ERROR "The 'code_gen' component was requested but not found. To
  enable this component, the SDK must be built with \
  INSTALL_CODE_GEN_SCRIPTS=ON")
endif()

include(${CMAKE_CURRENT_LIST_DIR}/cmake/anari_sdk_fetch_project.cmake)
include(${CMAKE_CURRENT_LIST_DIR}/cmake/cmake_project_commands.cmake)

if (TARGET anari::anari_viewer)
  set(anari_viewer_FOUND 1)
elseif (anari_FIND_REQUIRED_viewer AND @INSTALL_VIEWER_LIBRARY@)
  add_subdirectory(
    ${ANARI_DATAROOTDIR}/anari_viewer
    ${CMAKE_CURRENT_BINARY_DIR}/anari_viewer
  )
  set(anari_viewer_FOUND 1)
elseif(anari_FIND_REQUIRED_viewer)
  message(ERROR "The 'viewer' component was requested but not found. To
  enable this component, the SDK must be built with INSTALL_VIEWER_LIBRARY=ON")
endif()

check_required_components(@PROJECT_NAME@)
set(ANARI_FOUND TRUE)
